plugins {
    id 'com.github.johnrengelman.processes' version "0.5.0"
    id 'org.springdoc.openapi-gradle-plugin' version "1.6.0"
    id 'org.springframework.boot' version '2.7.8'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.asciidoctor.jvm.convert' version '2.4.0'
    id 'java'
    id "io.gatling.gradle" version "3.9.0.3"
}

group = 'com.liatrio'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    contractTestImplementation.extendsFrom(testImplementation)
    contractTestRuntimeOnly.extendsFrom(testRuntimeOnly)
    asciidoctorExt
}

sourceSets {

	gatling {
		java.srcDirs = ["src/performanceTest/java"]
		resources.srcDirs = ["src/performanceTest/resources"]
	}
	contractTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output

		java {
		}
	}
	functionalTest {
		java {
			java.srcDirs=["src/functionalTest/java"]
		}
	}
}

allprojects {
    tasks.withType(Test) {
        testLogging {
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
            events = ["passed", "skipped", "failed"]
        }
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-rest:2.7.8'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2:2.1.214'

    implementation 'io.swagger:swagger-annotations:1.6.9'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.14'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security:2.7.8'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
    }
    testImplementation 'org.springframework.security:spring-security-test'

    implementation 'jakarta.persistence:jakarta.persistence-api:2.2.3'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:9.0.71'
    implementation 'org.springframework.boot:spring-boot-autoconfigure:2.7.8'
    implementation 'org.springframework.boot:spring-boot:2.7.8'
    implementation 'org.springframework.data:spring-data-commons:2.7.7'
    implementation 'org.springframework.data:spring-data-jpa:2.7.7'
    implementation 'org.springframework.data:spring-data-rest-core:3.7.7'
    implementation 'org.springframework.security:spring-security-config:5.7.5'
    implementation 'org.springframework:spring-context:5.3.25'
    implementation 'org.springframework:spring-web:5.3.25'
    testImplementation 'com.vaadin.external.google:android-json:0.0.20131108.vaadin1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure:2.7.8'
    testImplementation 'org.springframework.boot:spring-boot-test:2.7.8'
    testImplementation 'org.springframework:spring-beans:5.3.25'
    testImplementation 'org.springframework:spring-test:5.3.25'

    contractTestImplementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.10'
    contractTestImplementation 'au.com.dius.pact.provider:junit5:4.4.5'


    functionalTestImplementation 'io.rest-assured:spring-mock-mvc:4.5.1'
    functionalTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    functionalTestImplementation 'org.springframework.security:spring-security-test'
    functionalTestImplementation 'org.junit.platform:junit-platform-suite-api:1.9.2'

    functionalTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    functionalTestImplementation 'com.google.code.gson:gson:2.10.1'
    functionalTestImplementation 'org.apache.johnzon:johnzon-core:1.2.19'

    asciidoctorExt 'org.asciidoctor:asciidoctorj:2.5.7'
    zinc 'org.scala-sbt:zinc_2.12:1.8.0'
}

springBoot {
  mainClass = 'com.liatrio.dojo.devopsknowledgeshareapi.Application'
}

bootJar {
   archiveFileName = 'devops-knowledge-share-api.jar'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()

}

task contractTest(type: Test) {
	description = "Run contract tests"
	group = "verification"

	testClassesDirs = sourceSets.contractTest.output.classesDirs
	classpath = sourceSets.contractTest.runtimeClasspath

	useJUnitPlatform()

	systemProperty "spring.profiles.active", "local"
	systemProperty "pact.provider.version", System.env.PACT_PROVIDER_VERSION ?: "0.0.0"
	systemProperty 'pactbroker.host', System.env.PACT_BROKER_URL ?: "localhost"
	systemProperty 'pactbroker.port', System.env.PACT_BROKER_PORT ?: "9292"
	systemProperty 'pactbroker.scheme', System.env.PACT_BROKER_SCHEME ?: 'http'
	systemProperty 'pactbroker.auth.username', System.env.PACT_BROKER_USERNAME ?: ""
	systemProperty 'pactbroker.auth.password', System.env.PACT_BROKER_PASSWORD ?: ""
	systemProperty 'pact.verifier.publishResults', true
}


task functionalTest(type: Test) {
	description = "Run functional tests"
	group = "verification"

	testClassesDirs = sourceSets.functionalTest.output.classesDirs
	classpath = sourceSets.functionalTest.runtimeClasspath

	useJUnitPlatform()

	systemProperties = System.properties

    testLogging {
        exceptionFormat = 'full'
        events = ["passed", "failed", "skipped"]
        showStandardStreams = true
    }

}

task setupLocalGitHooks(type: Copy){
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks')}
    from new File(rootProject.rootDir, 'scripts/pre-push')
    into { new File(rootProject.rootDir, '.git/hooks')}
    fileMode 0775
}
build.dependsOn setupLocalGitHooks

asciidoctor {
    configurations 'asciidoctorExt'
    inputs.dir snippetsDir
    dependsOn test
}

def awsCodeartifactAuthToken() {
    def token = System.env.CODEARTIFACT_AUTH_TOKEN
    def msg = 'env var CODEARTIFACT_AUTH_TOKEN not set'
    def ca_url = System.env.CODE_ARTIFACT_URL
    def fail = false
    if (!token) {
        if (fail) {
            throw new GradleException(msg)
        }
        msg = 'WARNING: ' + msg + ': Falling back to mavenCentral'
        println('-' * msg.length())
        println(msg)
        println('-' * msg.length())
    }
    if (!ca_url) {
        if (fail) {
            throw new GradleException(msg)
        }
        msg = 'WARNING: env var CODE_ARTIFACT_URL is not set in this environment'
        println('-' * msg.length())
        println(msg)
        println('-' * msg.length())
    }
    return token
}
